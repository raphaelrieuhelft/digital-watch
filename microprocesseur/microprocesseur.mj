const code_wordsize = 16
const code_addrsize = 9
const reg_addrsize = 5
const reg_wordsize = 12
const out_addrsize = 4
const out_wordsize = 7

(*MUX a b c = if a then b else c*)

power<n>(i) = (o:[n]) where
	if n = 1 then
		o = i
	else
		o = power<n-1>(i) . i
	end if
end where

mux_n<n>(c, a:[n], b:[n]) = (o:[n]) where
	if n = 0 then
		o = []
	else
		o_n1 = mux_n<n-1>(c, a[1..n-1], b[1..n-1]);
		o_n = mux(c, a[0], b[0]);
		o = o_n . o_n1
	end if
end where

halfadder(a,b)=(s,r) where
	r=a&b;
	s=a xor b
end where

(*increments by 1. s should be >=1*)
incr<s>(in:[s])=(out:[s], c_out) where
	if s = 1 then
		out = not(in[0]);
		c_out=in[0];
	else
		(out1, c1_out)=incr<s-1>(in[..s-2]);
		(out2, c_out)=halfadder(c1_out, in[s-1]);
		out=out1.out2
	end if
end where

(*increments by 2. s should be >=2*)
incr2<s>(in:[s])=(out:[s], c_out) where
	(out1, c_out)=incr<s-1>(in[1..]);
	out = in[0].out1
end where
	
	
PC(in:[code_addrsize])=(out:[code_addrsize]) where
	o0=reg(in[0]);
        o1=reg(in[1]);
	o2=reg(in[2]);
	o3=reg(in[3]);
	o4=reg(in[4]);
	o5=reg(in[5]);
	o6=reg(in[6]);
	o7=reg(in[7]);
	o8=reg(in[8]);
        out=o0.o1.o2.o3.o4.o5.o6.o7.o8.[]
end where

program(instr:[code_wordsize], J, branchJump)=(next_instr:[code_wordsize]) where
	imm(*taille code_addrsize*) = instr[4..12];
        pcOut = PC(Addr);
        (oi,of)  = incr<code_addrsize>(pcOut);
        (oi2, of2)= incr2<code_addrsize>(pcOut);
	JumpAddr=mux_n<code_addrsize>(J, imm, oi);
	Addr = mux_n<code_addrsize>(branchJump, oi2, JumpAddr);
	
	next_instr = rom<code_addrsize,code_wordsize>(pcOut)
end where

CtrlFlow(instr:[code_wordsize])=(A,D,E,F,G,I,J,K) where
	opcode = instr[0..3];
	F=opcode[0];
	E=opcode[1]; 
	G=opcode[2];
	A=opcode[3];
	D=not((E or G) or A);
	I=not(F or E);
	JK = mux(F,0,E);
	J = mux(G, 0, JK);
	K = mux(G, JK, 0);
end where


regRAM(ra1:[reg_addrsize], ra2:[reg_addrsize], wa:[reg_addrsize], wdata:[reg_wordsize], we)=(out1:[reg_wordsize], out2:[reg_wordsize]) where
	out1=ram<reg_addrsize, reg_wordsize>(ra1, we, wa, wdata);
	out2=ram<reg_addrsize, reg_wordsize>(ra2, we, wa, wdata)
end where

NotEqual<s>(in1:[s], in2:[s])=(out) where
	if s=0 then
		out = 0;
	else
		out1=NotEqual<s-1>(in1[1..], in2[1..]);
		out = out1 or (in1[0] xor in2[0])
	end if
end where

	
	
registers(instr:[code_wordsize], inImm:[reg_wordsize], A, D, E, F, G)=(out1:[reg_wordsize], outNeq) where
	rs(*:[reg_addrsize]*)=instr[4..8];
	rt(*:[reg_addrsize]*)=instr[9..13];
	immLi(*:[7]*)=instr[9..15];
	immLbi(*:[reg_wordsize]*)=instr[4..15];
	immLiExt(*:[reg_wordsize]*)=immLi.power<reg_wordsize-7>(0);
	immInstr(*:[reg_wordsize]*)=mux_n<reg_wordsize>(A, immLiExt, immLbi);
	imm(*:[reg_wordsize]*)=mux_n<reg_wordsize>(G, inImm, immInstr);
	k0(*:[reg_addrsize]*)=power<reg_addrsize>(1);
	wa1(*:[reg_addrsize]*)=mux_n<reg_addrsize>(E, rt, rs);
	wa(*:[reg_addrsize]*)=mux_n<reg_addrsize>(D, k0, wa1);
	(outincr(*:[reg_wordsize]*), c_out)=incr<reg_wordsize>(out1);
	outMod4(*:[2]*)=out1[0..1];
	outMod4Ext(*:[reg_wordsize]*)=outMod4.power<reg_wordsize-2>(0);
	outCalc(*:[reg_wordsize]*)=mux_n<reg_wordsize>(G, outincr, outMod4Ext);
	wdata(*:[reg_wordsize]*)=mux_n<reg_wordsize>(E, outCalc, imm);
	(out1(*:[reg_wordsize]*), out2(*:[reg_wordsize]*))=regRAM(rs, rt, wa, wdata, F);
	outNeq=NotEqual<reg_wordsize>(out1,out2)
end where


outRAM(instr:[code_wordsize], regOut:[reg_wordsize], G, I)=(out:[out_wordsize]) where
	immInstr(*:[out_addrsize]*)=instr[9..12];
	out4=regOut[0..3];
	DEC7(*:[out_wordsize]*)=rom<4,out_wordsize>(out4); (*ROM : Dec7*)
	outRaw(*:[out_wordsize]*)=regOut[0..6];
	wdata=mux_n<out_wordsize>(G, DEC7, outRaw);
	out=ram<out_addrsize, out_wordsize>(immInstr, I, immInstr, wdata)
end where

inputs(instr:[code_wordsize],in0, in1, in2, in3, in4, in5)=(inImm:[reg_wordsize]) where
	c0=instr[9];
	c1=instr[10];
	c2=instr[11];
	qp=mux(c0, in5, in4);
	is=mux(c0, in3, in2);
	sr=mux(c0, in1, in0);
	issr=mux(c1, is, sr);
	inBit=mux(c2, qp, issr);
	inImm=inBit.power<reg_wordsize-1>(0)
end where

main(in0, in1, in2, in3, in4, in5)=() where
	branchJump=mux(outNeq, 0, K);
	(A,D,E,F,G,I,J,K)=CtrlFlow(instr);
	inImm(*:[reg_wordsize]*)=inputs(instr, in0, in1, in2, in3, in4, in5);
	(regOut(*:[reg_wordsize]*), outNeq)=registers(instr, inImm, A,D,E,F,G);
	output(*:[out_wordsize]*)=outRAM(instr, regOut, G, I);
        instr(*:[code_wordsize]*)=program(instr, J, branchJump)
end where


